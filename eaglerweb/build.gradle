
plugins {
	id "java-library"
	id "com.gradleup.shadow" version "8.3.6"
}

configurations {
	platformBukkit
	platformBungee
	platformVelocity
}

dependencies {
	platformBukkit project(":eaglerweb:eaglerweb-platform-bukkit")
	platformBungee project(":eaglerweb:eaglerweb-platform-bungee")
	platformVelocity project(":eaglerweb:eaglerweb-platform-velocity")
	compileOnly project(":api")
	compileOnly(libs.guava)
	compileOnly(libs.gson)
	compileOnly(libs.slf4j)
}

tasks.named("shadowJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for all supported platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBukkit,
		project.configurations.platformBungee,
		project.configurations.platformVelocity
	]
	archiveFileName = "EaglerWeb.jar"
}

tasks.register("shadowJarBukkit", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Bukkit platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBukkit
	]
	archiveFileName = "EaglerWeb-Bukkit.jar"
}

tasks.register("shadowJarBungee", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Bungee platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBungee
	]
	archiveFileName = "EaglerWeb-Bungee.jar"
}

tasks.register("shadowJarVelocity", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Velocity platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformVelocity
	]
	archiveFileName = "EaglerWeb-Velocity.jar"
}
