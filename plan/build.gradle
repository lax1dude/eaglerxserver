
plugins {
	id "java-library"
	id "com.gradleup.shadow" version "8.3.6"
}

configurations {
	platformBukkit
	platformBungee
	platformVelocity
}

repositories {
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	platformBukkit project(":plan:plan-platform-bukkit")
	platformBungee project(":plan:plan-platform-bungee")
	platformVelocity project(":plan:plan-platform-velocity")
	compileOnly project(":api")
	compileOnly "com.github.plan-player-analytics:Plan:5.6.2965"
}

tasks.named("shadowJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for all supported platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBukkit,
		project.configurations.platformBungee,
		project.configurations.platformVelocity
	]
	archiveFileName = "EaglerXPlan.jar"
}

tasks.register("shadowJarBukkit", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Bukkit platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBukkit
	]
	archiveFileName = "EaglerXPlan-Bukkit.jar"
}

tasks.register("shadowJarBungee", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Bungee platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBungee
	]
	archiveFileName = "EaglerXPlan-Bungee.jar"
}

tasks.register("shadowJarVelocity", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Velocity platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformVelocity
	]
	archiveFileName = "EaglerXPlan-Velocity.jar"
}
