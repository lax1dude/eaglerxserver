
plugins {
	id "java-library"
	id "com.gradleup.shadow" version "8.3.6"
}

configurations {
	platformBukkit
	platformBungee
	platformVelocity
}

dependencies {
	platformBukkit project(":rewind_v1_5:rewind_v1_5-platform-bukkit")
	platformBungee project(":rewind_v1_5:rewind_v1_5-platform-bungee")
	platformVelocity project(":rewind_v1_5:rewind_v1_5-platform-velocity")
	compileOnly project(":api")
	compileOnly(libs.bundles.netty.all)
	compileOnly(libs.guava)
	compileOnly(libs.slf4j)
}

tasks.named("shadowJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for all supported platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBukkit,
		project.configurations.platformBungee,
		project.configurations.platformVelocity
	]
	archiveFileName = "EaglerXRewind.jar"
}

tasks.register("shadowJarBukkit", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Bukkit platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBukkit
	]
	archiveFileName = "EaglerXRewind-Bukkit.jar"
}

tasks.register("shadowJarBungee", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Bungee platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformBungee
	]
	archiveFileName = "EaglerXRewind-Bungee.jar"
}

tasks.register("shadowJarVelocity", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	group "shadow"
	description "Builds the JAR for Velocity platforms"
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.platformVelocity
	]
	archiveFileName = "EaglerXRewind-Velocity.jar"
}
